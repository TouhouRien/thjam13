import "util.gr"
import "map.gr"

export func editLevel() {
    initMap(15, 15);
    var scene = @Level.findSceneByName("level")?;

    createDrawer();

    @Input.addAction("click");
    @Input.addActionEvent("click", @InputEvent.mouseButton(MouseButton.left, KeyState.pressed, 1, @Vec2f.zero, @Vec2f.zero));

    loop yield {
        if (@Input.isActionActivated("click")) {
            var x: int = (scene.mousePosition.x / 32) as<int>;
            var y: int = (scene.mousePosition.y / 32) as<int>;
            //print(x ~","~ y as<int>);
            setMapId(x, y, 2);
        }
    }
}

func createDrawer() {
    var width = 400f;
    var height = 100f;

    var drawer = @UIElement;
    drawer.size = @Vec2f(width, height);
    drawer.setAlign(UIAlignX.center, UIAlignY.bottom);
    drawer.position = @Vec2f(0f, 35f);

    var bg = @RoundedRectangle.fill(width, height, 8f);
    bg.anchor = @Vec2f.zero;
    bg.color = @Color(0.08f, 0.11f, 0.18f);
    drawer.addImage(bg);

    @UI.add(drawer);
}