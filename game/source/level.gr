import "map.gr"
import "util.gr"

enum HaniwaType {
    mayumi;
    seija;
    cirno;
    saki;
    momoyo;
}

var haniwaTypeList: [string];
var haniwaTypeSpawned: [bool];
var spawnAreas: [Vec2i];
var haniwas: [Haniwa];
var won = false;

class Haniwa {
    var entity: Entity;
    var animation: Animation;
    var actor: Actor;
    var baseSpeed: Vec2f;
    var targetSpeed: Vec2f;
    var speed: Vec2f;
    var offset: Vec2i;
    var direction: float;
    var isPlayer: bool;
    var onGround: bool;
    var isJumping: bool;
    var turnedOn: bool;
}

class Goal {
    var entity: Entity;
    var animation: Animation;
    var actor: Actor;
}

export func initLevel(levelCount: int) {
    won = false;
    @UI.clear();

    {
        var transition = @UIElement;
        transition.size = @App.size;
        var rect = @Rectangle.fill(transition.size.x, transition.size.y);
        rect.color = @Color.black;
        rect.anchor = @Vec2f.zero;
        transition.addImage(rect);
        transition.setAlign(UIAlignX.right, UIAlignY.center);

        var startState = @UIState("start");
        transition.addState(startState);

        var endState = @UIState("end");
        endState.scale = @Vec2f(0f, 1f);
        endState.time = 30;
        endState.spline = Spline.sineInOut;
        transition.addState(endState);

        transition.setState("start");
        transition.runState("end");

        transition.addEventListener("state", event {
            if(transition.getState() == "end") {
                transition.remove();
            }
        });

        @UI.add(transition);
    }

    spawnAreas = default<[Vec2i]>;
    haniwaTypeList = default<[string]>;
    haniwaTypeSpawned = default<[bool]>;
    haniwas = default<[Haniwa]>;
    loop(i, HaniwaType.momoyo as<int> + 1) {
        haniwaTypeList ~= i as<HaniwaType> as<string>;
        haniwaTypeSpawned ~= false;
    }

    var levelName = "level" ~ (levelCount as<string>);
    var goalPos = @Vec2i.zero;

    var music = @Music("tinyClayBuddies");
    music.playTrack(2f);

    var tilemap = @Tilemap(levelName);
    var map = initMap(tilemap, tilemap.width, tilemap.height);

    if (levelName == "level1") {
        spawnAreas ~= @Vec2i(1, 8);
        spawnAreas ~= @Vec2i(2, 8);
        spawnAreas ~= @Vec2i(3, 8);
        spawnAreas ~= @Vec2i(10, 8);
        spawnAreas ~= @Vec2i(6, 3);
        goalPos = @Vec2i(15, 5);
    } else if (levelName == "level2") {
        spawnAreas ~= @Vec2i(1, 8);
        spawnAreas ~= @Vec2i(4, 1);
        spawnAreas ~= @Vec2i(5, 4);
        spawnAreas ~= @Vec2i(10, 8);
        spawnAreas ~= @Vec2i(9, 3);
        goalPos = @Vec2i(15, 5);
    }

    var scene = @Level.findSceneByName("level")?;
    updateMap(scene, map);

    var goal = initGoal(scene, map, goalPos);

    haniwaSpawner(scene, map, goal, spawnAreas, false);

    loop yield {
        /*if(@Input.isDown(KeyButton.space)) {
            won = true;
        }*/
        if(won) {
            wait(5);
            won = false;

            var transition = @UIElement;
            transition.size = @App.size;
            var rect = @Rectangle.fill(transition.size.x, transition.size.y);
            rect.color = @Color.black;
            rect.anchor = @Vec2f.zero;
            transition.addImage(rect);
            transition.setAlign(UIAlignX.left, UIAlignY.center);

            var startState = @UIState("start");
            startState.scale = @Vec2f(0f, 1f);
            transition.addState(startState);

            var endState = @UIState("end");
            endState.time = 30;
            endState.spline = Spline.sineInOut;
            transition.addState(endState);

            transition.setState("start");
            transition.runState("end");

            @UI.clear();
            @UI.add(transition);
            wait(30);

            initLevel(levelCount + 1);
        }
    }
}

var currentDrawer: UIElement?;
func createDrawer(scene: Scene, map: Map, goal: Goal, spawnTile: Vec2i) {
    var width = 700f;
    var height = 100f;

    if(currentDrawer) {
        var drawer = currentDrawer?;
        drawer.isEnabled = false;
        drawer.runState("hidden");
        wait(10);
        drawer.remove();
    }

    var drawer = @UIElement;
    drawer.size = @Vec2f(width, height);
    drawer.setAlign(UIAlignX.center, UIAlignY.bottom);
    drawer.position = @Vec2f(0f, 35f);
    currentDrawer = drawer;

    var visibleState = @UIState("visible");
    visibleState.time = 20;
    visibleState.spline = Spline.sineInOut;
    drawer.addState(visibleState);

    var hiddenState = @UIState("hidden");
    hiddenState.scale = @Vec2f(1f, 0f);
    hiddenState.offset = @Vec2f(0f, -50f);
    hiddenState.time = 10;
    hiddenState.spline = Spline.sineInOut;
    drawer.addState(hiddenState);

    drawer.setState("hidden");
    drawer.runState("visible");

    var bg = @RoundedRectangle.fill(width, height, 8f);
    bg.anchor = @Vec2f.zero;
    bg.color = @Color(0.08f, 0.11f, 0.18f);
    drawer.addImage(bg);

    var hbox = @HBox;
    hbox.spacing = 16f;
    drawer.addUI(hbox);

    {
        var exitBtn = @UIElement;
        var sprite = @Sprite("exitbtn");
        sprite.anchor = @Vec2f.zero;
        exitBtn.size = sprite.size;
        exitBtn.addImage(sprite);
        exitBtn.setAlign(UIAlignX.right, UIAlignY.top);
        exitBtn.position = @Vec2f(32f, 32f);
        exitBtn.addEventListener("click", event {
            haniwaSpawner(scene, map, goal, spawnAreas, false);
            drawer.isEnabled = false;
            drawer.runState("hidden");
            wait(10);
            drawer.remove();
        });
        drawer.addUI(exitBtn);
    }

    for(type, haniwaTypeList) {
        var haniwaType = type as<HaniwaType>;
        if(haniwaTypeSpawned[haniwaType as<int>])
            continue;

        var btn = @UIElement;
        btn.size = @Vec2f(100f, 100f);

        var nameSprite = @Sprite(type ~ ".name");
        nameSprite.anchor = @Vec2f(0.5f, 1f);
        nameSprite.position = @Vec2f(btn.size.x / 2f, btn.size.y);

        var rect = @RoundedRectangle.fill(btn.size.x, btn.size.y, 8f);
        rect.anchor = @Vec2f.zero;
        rect.color = @Color.red;
        btn.addImage(rect);

        var sprite = @Sprite(type);
        sprite.anchor = @Vec2f.half;
        sprite.position = @Vec2f(32f, 32f);

        btn.addImage(sprite);
        btn.addImage(nameSprite);

        btn.addEventListener("click", event {
            if(haniwaTypeSpawned[haniwaType as<int>])
                return;

            var areas: [Vec2i];
            for(area, spawnAreas) {
                if(area.x != spawnTile.x || area.y != spawnTile.y) {
                    areas ~= area;
                }
            }
            spawnAreas = areas;
            haniwaSpawner(scene, map, goal, spawnAreas, false);
            
            haniwaTypeSpawned[haniwaType as<int>] = true;

            var turnedOn = false;
            if (haniwas.size == 4) {
                turnedOn = true;
            }

            var spawnPos = spawnTile + @Vec2i(-1, 0);
            var haniwa = initHaniwa(scene, map, type, spawnPos, turnedOn);
            haniwas ~= haniwa;
            updateHaniwa(map, haniwa);
            
            switch(type)
            case("mayumi") {
                updateJump(scene, map, haniwa);
                updateGoal(map, haniwa, goal);
            }
            case("seija") {
                updateSeija(scene, map, haniwa);
            }
            case("cirno") {
                updateCirno(scene, map, haniwa);
            }
            case("saki") {
                updateSaki(scene, map, haniwa);
            }
            case("momoyo") {
                updateMomoyo(scene, map, haniwa);
            }

            drawer.isEnabled = false;
            drawer.runState("hidden");
            wait(30);
            drawer.remove();
            currentDrawer = null;
        });

        btn.addEventListener("mouseenter", event {
            rect.color = @Color.blue;
        });

        btn.addEventListener("mouseleave", event {
            rect.color = @Color.red;
        });

        hbox.addUI(btn);
    }

    @UI.add(drawer);
}

func haniwaSpawner(scene: Scene, map: Map, goal: Goal, areas: [Vec2i], isSelected: bool) {
    scene.clearUI();

    for(area, areas) {
        var pos = area + @Vec2i(0, 1);

        var btn = @UIElement;
        btn.size = @Vec2f(64f, 64f);
        btn.position = (pos * 64 - 32) as<Vec2f>;

        var rect = @RoundedRectangle.fill(btn.size.x - 2f, btn.size.y - 2f, 8f);
        rect.anchor = @Vec2f.half;
        rect.position = btn.size / 2f;
        rect.color = @Color.red;
        rect.isVisible = false;
        btn.addImage(rect);

        var sprite = @Sprite("cursor");
        sprite.anchor = @Vec2f.zero;
        btn.addImage(sprite);

        scene.addUI(btn);

        if(isSelected) {
            sprite.color = @Color.blue;
        }
        else {
            btn.addEventListener("click", event {
                haniwaSpawner(scene, map, goal, [area], true);
                createDrawer(scene, map, goal, area);
            });

            btn.addEventListener("mouseenter", event {
                rect.isVisible = true;
            });
            btn.addEventListener("mouseleave", event {
                rect.isVisible = false;
            });
        }
    }
}

func initHaniwa(scene: Scene, map: Map, name: string, startPos: Vec2i, turnedOn: bool)(Haniwa) {
    var offset = @Vec2i(0, 10);

    var actor = @Actor;
    actor.position = mapToWorldPosition(map, startPos) - offset;
    actor.hitbox = @Vec2i(24, 32);
    scene.addActor(actor);

    var entity = @Entity;
    scene.addEntity(entity);
    actor.entity = entity;

    var animation = @Animation(name);
    entity.addImage(animation);

    var haniwa = @Haniwa {
        entity = entity;
        animation = animation;
        actor = actor;
        baseSpeed = @Vec2f(2f, 5f);
        targetSpeed = @Vec2f(2f, 5f);
        speed = @Vec2f.zero;
        offset = offset;
        direction = 1f;
        turnedOn = turnedOn;
    };

    return haniwa;
}

func resetSpeed(haniwa: Haniwa) {
    haniwa.targetSpeed.x = haniwa.direction * haniwa.baseSpeed.x;
    haniwa.targetSpeed.y = haniwa.baseSpeed.y;
}

func initGoal(scene: Scene, map: Map, startPos: Vec2i)(Goal) {
    var actor = @Actor;
    actor.position = mapToWorldPosition(map, startPos);
    actor.hitbox = @Vec2i(24, 32);
    scene.addActor(actor);

    var entity = @Entity;
    scene.addEntity(entity);
    actor.entity = entity;

    var animation = @Animation("goal");
    entity.addImage(animation);

    var goal = @Goal {
        entity = entity;
        animation = animation;
        actor = actor;
    };

    return goal;
}

task updateHaniwa(map: Map, haniwa: Haniwa) {
    until yield(won) {
        if (!haniwa.turnedOn) {
            haniwa.targetSpeed.x = 0f;
        } else {
            loop (otherId, haniwas.size) {
                var other = haniwas[otherId];
                if (other.turnedOn) {
                    continue;
                }

                var haniwaPos = worldToMapPosition(map, haniwa.actor.position + haniwa.offset);
                var otherPos  = worldToMapPosition(map, other.actor.position + other.offset);

                if ((haniwaPos.x + 1 == otherPos.x || haniwaPos.x - 1 == otherPos.x) && haniwaPos.y == otherPos.y) {
                    var startSFX = @Sound("start");
                    startSFX.play();

                    other.turnedOn = true;
                    other.resetSpeed();
                }
            }
        }

        if (haniwa.isPlayer) {
            haniwa.speed.x = approach(haniwa.speed.x, @Input.getActionAxis("left", "right") * 3f, 0.5f);
        } else {
            haniwa.speed.x = approach(haniwa.speed.x, haniwa.targetSpeed.x, 0.5f);
        }

        haniwa.speed.y = approach(haniwa.speed.y, haniwa.targetSpeed.y, 0.5f);

        if (haniwa.onGround) {
            if (haniwa.isPlayer && @Input.isActionActivated("jump")) {
                haniwa.actor.dismount();
                haniwa.onGround = false;
                haniwa.targetSpeed.y = -10f;
            }
        }

        var collision = haniwa.actor.moveX(haniwa.speed.x);
        if (collision && haniwa.onGround) {
            haniwa.speed.x = 0f;
            haniwa.targetSpeed.x = -haniwa.targetSpeed.x;
            haniwa.animation.flipX = !haniwa.animation.flipX;
            haniwa.direction *= -1f;
        }

        var collision = haniwa.actor.moveY(haniwa.speed.y);
        if (collision) {
            var collision = collision?;

            if (collision.direction.y > 0) {
                haniwa.onGround = true;
                haniwa.actor.mount(collision.solid);
            }

            if (collision.direction.y < 0) {
                haniwa.onGround = false;
                haniwa.resetSpeed();
            }
        } else {
            haniwa.onGround = false;
        }
    }
}

task updateJump(scene: Scene, map: Map, haniwa: Haniwa) {
    until yield(won) {
        var tilePos = worldToMapPosition(map, haniwa.actor.position + haniwa.offset);

        var belowPos = @Vec2i(tilePos.x, tilePos.y + 1);
        var belowId = getMapId(belowPos.x, belowPos.y);

        var downRightPos = @Vec2i(tilePos.x + 1, tilePos.y + 1);
        var downRightId = getMapId(downRightPos.x, downRightPos.y);

        if (haniwa.onGround) {
            if (belowId == TileType.up && !haniwa.isJumping) {
                haniwa.isJumping = true;
                var jumpSFX = @Sound("jump");
                jumpSFX.play();

                haniwa.targetSpeed.x = 0f;
                haniwa.targetSpeed.y = -10f;
            }
        }
        else if (belowId != TileType.up && isSolid(downRightId)) {
            haniwa.isJumping = false;
            haniwa.resetSpeed();
        }
    }
}

task updateCirno(scene: Scene, map: Map, haniwa: Haniwa) {
    updateJump(scene, map, haniwa);

    until yield(won) {
        var tilePos = worldToMapPosition(map, haniwa.actor.position + haniwa.offset);
        var downRightPos = @Vec2i(tilePos.x + 1, tilePos.y + 1);
        var downRightId = getMapId(downRightPos.x, downRightPos.y);

        if (downRightId == TileType.water) {
            var freezeSFX = @Sound("freeze");
            freezeSFX.play();

            waterToIce(scene, map, downRightPos);
        }
    }
}

task updateSaki(scene: Scene, map: Map, haniwa: Haniwa) {
    updateJump(scene, map, haniwa);

    until yield(won) {
        var tilePos = worldToMapPosition(map, haniwa.actor.position + haniwa.offset);

        var downRightPos = @Vec2i(tilePos.x + 1, tilePos.y + 1);
        var downRightId = getMapId(downRightPos.x, downRightPos.y);

        var shouldJump = false;
        var upPos = @Vec2i(tilePos.x, tilePos.y - 1);
        do {
            var rightPos = @Vec2i(upPos.x + 1, tilePos.y);
            var rightId = getMapId(rightPos.x, rightPos.y);
            if (isSolid(rightId)) {
                shouldJump = true;
            }

            upPos.y -= 1;
        } while (upPos.y > 0 && shouldJump == false);

        if (haniwa.onGround && !haniwa.isJumping && shouldJump) {
            haniwa.isJumping = true;
            //var jumpSFX = @Sound("jump");
            //jumpSFX.play();

            haniwa.targetSpeed.x = 0f;
            haniwa.targetSpeed.y = -10f;
        } else if (isSolid(downRightId)) {
            haniwa.isJumping = false;
            haniwa.resetSpeed();
        }
    }
}

task updateMomoyo(scene: Scene, map: Map, haniwa: Haniwa) {
    updateJump(scene, map, haniwa);

    until yield(won) {
        var tilePos = worldToMapPosition(map, haniwa.actor.position + haniwa.offset);
        var belowPos = @Vec2i(tilePos.x, tilePos.y + 1);
        var belowId = getMapId(belowPos.x, belowPos.y);

        if (haniwa.onGround && haniwa.turnedOn) {
            if (belowId == TileType.rock) {
                var digSFX = @Sound("dig");
                digSFX.play();

                haniwa.targetSpeed.x = 0f;
                setMapId(belowPos.x, belowPos.y, TileType.air);
            } else if(belowId != TileType.up && isSolid(belowId)) {
                haniwa.resetSpeed();
            }
        }
    }
}

task updateSeija(scene: Scene, map: Map, haniwa: Haniwa) {
    var trigger = false;
    until yield(won) {
        var tilePos = worldToMapPosition(map, haniwa.actor.position + haniwa.offset);
        var belowPos = @Vec2i(tilePos.x, tilePos.y + 1);
        var belowId = getMapId(belowPos.x, belowPos.y);

        if (haniwa.onGround && !haniwa.isJumping) {
            if (!trigger && belowId == TileType.down) {
                var flipSFX = @Sound("flip");
                flipSFX.play();
                setMapId(belowPos.x, belowPos.y, TileType.up);
                trigger = true;
            } else if (!trigger && belowId == TileType.up) {
                var flipSFX = @Sound("flip");
                flipSFX.play();
                setMapId(belowPos.x, belowPos.y, TileType.down);
                trigger = true;
            }
            
            if (belowId != TileType.down && belowId != TileType.up) {
                trigger = false;
            }
        }
    }
}

task updateGoal(map: Map, player: Haniwa, goal: Goal) {
    loop yield {
        var playerPos = worldToMapPosition(map, player.actor.position + player.offset);
        var goalPos   = worldToMapPosition(map, goal.actor.position);

        if (!won && playerPos.x == goalPos.x && playerPos.y == goalPos.y) {
            var winSFX = @Sound("win");
            winSFX.play();
            won = true;
            die;
        }
    }
}